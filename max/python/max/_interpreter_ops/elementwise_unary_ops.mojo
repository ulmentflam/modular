# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #

"""Mojo kernel wrappers for unary elementwise MO interpreter operations.

This module contains unary all-dtype ops (Negative, Abs, Relu, Ceil, Floor,
Round), unary float-only ops (Exp, Log, Log1p, Sqrt, Rsqrt, Tanh, ATanh, Sin,
Cos, Erf, Trunc), the activation ops (Relu backing `mo.relu`, and Sigmoid,
Silu, Gelu, GeluTanh, GeluQuick backing `mo.sigmoid`/`mo.silu`/`mo.gelu`/
`mo.gelu_tanh`/`mo.gelu_quick`), unary boolean ops (Not), and unary predicate
ops (IsNan, IsInf).
"""

from std.os import abort
from std.gpu.host import DeviceContext
from std.python import PythonObject
from std.python.bindings import PythonModuleBuilder
from std.sys.info import has_accelerator, simd_width_of

from std.algorithm.functional import elementwise, IndexList
from std.reflection import reflect

from extensibility import ElementwiseUnaryOp, ElementwiseUnaryMixedOp
from builtin_kernels import (
    Negative,
    Abs,
    Ceil,
    Floor,
    Round,
    Exp,
    Log,
    Log1p,
    Sqrt,
    Rsqrt,
    Tanh,
    ATanh,
    Sin,
    Cos,
    Erf,
    Trunc,
    Not,
    IsNan,
    IsInf,
)
from nn.activations import (
    gelu as _gelu,
    gelu_quick as _gelu_quick,
    gelu_tanh as _gelu_tanh,
    relu as _relu,
    sigmoid as _sigmoid,
    silu as _silu,
)

from op_utils import _get_dtype, _get_buffer_ptr, _get_size, _get_ctx


# Activation ops. Each activation has its own dedicated op (`mo.relu`,
# `mo.gelu`, `mo.gelu_tanh`, `mo.gelu_quick`, `mo.sigmoid`, `mo.silu`); the
# interpreter exposes one struct per activation, registered against its op type
# in `_interpreter_ops/__init__.py`.
struct Relu(ElementwiseUnaryOp):
    @staticmethod
    def elementwise[
        dtype: DType, width: SIMDSize
    ](x: SIMD[dtype, width]) -> SIMD[dtype, width]:
        return _relu(x)


struct Sigmoid(ElementwiseUnaryOp):
    @staticmethod
    def elementwise[
        dtype: DType, width: SIMDSize
    ](x: SIMD[dtype, width]) -> SIMD[dtype, width]:
        return _sigmoid(x)


struct Silu(ElementwiseUnaryOp):
    @staticmethod
    def elementwise[
        dtype: DType, width: SIMDSize
    ](x: SIMD[dtype, width]) -> SIMD[dtype, width]:
        return _silu(x)


struct Gelu(ElementwiseUnaryOp):
    @staticmethod
    def elementwise[
        dtype: DType, width: SIMDSize
    ](x: SIMD[dtype, width]) -> SIMD[dtype, width]:
        return _gelu(x)


struct GeluTanh(ElementwiseUnaryOp):
    @staticmethod
    def elementwise[
        dtype: DType, width: SIMDSize
    ](x: SIMD[dtype, width]) -> SIMD[dtype, width]:
        return _gelu_tanh(x)


struct GeluQuick(ElementwiseUnaryOp):
    @staticmethod
    def elementwise[
        dtype: DType, width: SIMDSize
    ](x: SIMD[dtype, width]) -> SIMD[dtype, width]:
        return _gelu_quick(x)


# Unary elementwise operations (all dtypes)
comptime UNARY_ELEMENTWISE_OPS = TypeList.of[
    Trait=ElementwiseUnaryOp, Negative, Abs, Relu, Ceil, Floor, Round
]()

# Unary elementwise operations (float only)
comptime UNARY_FLOAT_ONLY_OPS = TypeList.of[
    Trait=ElementwiseUnaryOp,
    Exp,
    Log,
    Log1p,
    Sqrt,
    Rsqrt,
    Tanh,
    ATanh,
    Sin,
    Cos,
    Erf,
    Trunc,
    Sigmoid,
    Silu,
    Gelu,
    GeluTanh,
    GeluQuick,
]()

# Unary mixed-type predicate operations (float input -> bool output)
comptime UNARY_PREDICATE_OPS = TypeList.of[
    Trait=ElementwiseUnaryMixedOp, IsNan, IsInf
]()

# =============================================================================
# GPU Support Configuration
# =============================================================================


def _is_gpu_allowed_unary_op[op: ElementwiseUnaryOp]() -> Bool:
    """Check if a unary op is allowed on GPU at compile time."""
    comptime name = reflect[op].base_name()
    # Basic ops, float ops, and boolean ops that work on GPU
    # Note: ATanh, Log1p, Erf use libm and don't work on GPU
    return (
        name == "Negative"
        or name == "Abs"
        or name == "Relu"
        or name == "Ceil"
        or name == "Floor"
        or name == "Round"
        or name == "Trunc"
        or name == "Exp"
        or name == "Log"
        or name == "Sqrt"
        or name == "Rsqrt"
        or name == "Tanh"
        or name == "Sin"
        or name == "Cos"
        or name == "Not"
        # Activation ops (Gelu uses erf via libm and is CPU-only, like Erf).
        or name == "Sigmoid"
        or name == "Silu"
        or name == "GeluTanh"
        or name == "GeluQuick"
    )


def _is_gpu_allowed_mixed_unary_op[op: ElementwiseUnaryMixedOp]() -> Bool:
    """Check if a mixed-type unary op is allowed on GPU at compile time."""
    comptime name = reflect[op].base_name()
    return name == "IsNan" or name == "IsInf"


# =============================================================================
# Python bindings
# =============================================================================


@export
def PyInit_elementwise_unary_ops() -> PythonObject:
    """Create a Python module with unary elementwise kernel function bindings.
    """
    try:
        var b = PythonModuleBuilder("elementwise_unary_ops")

        # Unary elementwise operations
        comptime for i in range(UNARY_ELEMENTWISE_OPS.size):
            comptime op = UNARY_ELEMENTWISE_OPS[i]
            comptime name = reflect[op].base_name()
            comptime docstring = StaticString("Elementwise " + name)
            b.def_function[unary_elementwise_dispatcher[op]](
                name, docstring=docstring
            )

        # Unary float-only operations
        comptime for i in range(UNARY_FLOAT_ONLY_OPS.size):
            comptime op = UNARY_FLOAT_ONLY_OPS[i]
            comptime name = reflect[op].base_name()
            comptime docstring = StaticString(
                "Elementwise " + name + " (float only)"
            )
            b.def_function[unary_elementwise_dispatcher[op, float_only=True]](
                name, docstring=docstring
            )

        # Unary boolean operation
        b.def_function[unary_bool_dispatcher[Not]](
            "Not", docstring="Elementwise Not (bool only)"
        )

        # Unary predicate operations (float -> bool)
        comptime for i in range(UNARY_PREDICATE_OPS.size):
            comptime op = UNARY_PREDICATE_OPS[i]
            comptime name = reflect[op].base_name()
            comptime docstring = StaticString(
                "Elementwise " + name + " predicate (float -> bool)"
            )
            b.def_function[unary_predicate_dispatcher[op]](
                name, docstring=docstring
            )

        return b.finalize()
    except e:
        abort(t"failed to create elementwise unary op bindings module: {e}")


# =============================================================================
# Dispatchers
# =============================================================================


def unary_elementwise_dispatcher[
    op: ElementwiseUnaryOp, *, float_only: Bool = False
](
    out_buffer: PythonObject,
    in_buffer: PythonObject,
    device_context_ptr: PythonObject,
) raises:
    """Unary elementwise operation dispatcher (all dtypes).

    Args:
        out_buffer: The output buffer object.
        in_buffer: The input buffer object.
        device_context_ptr: Device context pointer.
    """
    var dtype = _get_dtype(in_buffer)
    var size = _get_size(out_buffer)
    var ctx = _get_ctx(device_context_ptr)

    comptime if float_only:
        if dtype == DType.float16:
            unary_elementwise_op[op, DType.float16](
                _get_buffer_ptr[DType.float16](out_buffer),
                _get_buffer_ptr[DType.float16](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.float32:
            unary_elementwise_op[op, DType.float32](
                _get_buffer_ptr[DType.float32](out_buffer),
                _get_buffer_ptr[DType.float32](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.float64:
            unary_elementwise_op[op, DType.float64](
                _get_buffer_ptr[DType.float64](out_buffer),
                _get_buffer_ptr[DType.float64](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.bfloat16:
            unary_elementwise_op[op, DType.bfloat16](
                _get_buffer_ptr[DType.bfloat16](out_buffer),
                _get_buffer_ptr[DType.bfloat16](in_buffer),
                size,
                ctx,
            )
        else:
            raise Error(
                "Unsupported dtype for unary elementwise operation: "
                + String(dtype)
            )
    else:
        if dtype == DType.int8:
            unary_elementwise_op[op, DType.int8](
                _get_buffer_ptr[DType.int8](out_buffer),
                _get_buffer_ptr[DType.int8](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.int16:
            unary_elementwise_op[op, DType.int16](
                _get_buffer_ptr[DType.int16](out_buffer),
                _get_buffer_ptr[DType.int16](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.int32:
            unary_elementwise_op[op, DType.int32](
                _get_buffer_ptr[DType.int32](out_buffer),
                _get_buffer_ptr[DType.int32](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.int64:
            unary_elementwise_op[op, DType.int64](
                _get_buffer_ptr[DType.int64](out_buffer),
                _get_buffer_ptr[DType.int64](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.uint8:
            unary_elementwise_op[op, DType.uint8](
                _get_buffer_ptr[DType.uint8](out_buffer),
                _get_buffer_ptr[DType.uint8](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.uint16:
            unary_elementwise_op[op, DType.uint16](
                _get_buffer_ptr[DType.uint16](out_buffer),
                _get_buffer_ptr[DType.uint16](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.uint32:
            unary_elementwise_op[op, DType.uint32](
                _get_buffer_ptr[DType.uint32](out_buffer),
                _get_buffer_ptr[DType.uint32](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.uint64:
            unary_elementwise_op[op, DType.uint64](
                _get_buffer_ptr[DType.uint64](out_buffer),
                _get_buffer_ptr[DType.uint64](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.float16:
            unary_elementwise_op[op, DType.float16](
                _get_buffer_ptr[DType.float16](out_buffer),
                _get_buffer_ptr[DType.float16](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.float32:
            unary_elementwise_op[op, DType.float32](
                _get_buffer_ptr[DType.float32](out_buffer),
                _get_buffer_ptr[DType.float32](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.float64:
            unary_elementwise_op[op, DType.float64](
                _get_buffer_ptr[DType.float64](out_buffer),
                _get_buffer_ptr[DType.float64](in_buffer),
                size,
                ctx,
            )
        elif dtype == DType.bfloat16:
            unary_elementwise_op[op, DType.bfloat16](
                _get_buffer_ptr[DType.bfloat16](out_buffer),
                _get_buffer_ptr[DType.bfloat16](in_buffer),
                size,
                ctx,
            )
        else:
            raise Error(
                "Unsupported dtype for unary elementwise operation: "
                + String(dtype)
            )


def unary_bool_dispatcher[
    op: ElementwiseUnaryOp
](
    out_buffer: PythonObject,
    in_buffer: PythonObject,
    device_context_ptr: PythonObject,
) raises:
    """Unary boolean operation dispatcher (bool only).

    Args:
        out_buffer: The output buffer object.
        in_buffer: The input buffer object.
        device_context_ptr: Device context pointer.
    """
    var dtype = _get_dtype(in_buffer)

    if dtype == DType.bool:
        unary_elementwise_op[op, DType.bool](
            _get_buffer_ptr[DType.bool](out_buffer),
            _get_buffer_ptr[DType.bool](in_buffer),
            _get_size(out_buffer),
            _get_ctx(device_context_ptr),
        )
    else:
        raise Error(
            "Boolean operation requires bool dtype, got: " + String(dtype)
        )


def unary_predicate_dispatcher[
    op: ElementwiseUnaryMixedOp
](
    out_buffer: PythonObject,
    in_buffer: PythonObject,
    device_context_ptr: PythonObject,
) raises:
    """Unary predicate operation dispatcher (float input -> bool output).

    Args:
        out_buffer: The output buffer object (uint8/bool).
        in_buffer: The input buffer object (float).
        device_context_ptr: Device context pointer.
    """
    var dtype = _get_dtype(in_buffer)
    var out_ptr = _get_buffer_ptr[DType.bool](out_buffer)
    var size = _get_size(out_buffer)
    var ctx = _get_ctx(device_context_ptr)

    if dtype == DType.float16:
        unary_mixed_op[op, DType.float16, DType.bool](
            out_ptr,
            _get_buffer_ptr[DType.float16](in_buffer),
            size,
            ctx,
        )
    elif dtype == DType.float32:
        unary_mixed_op[op, DType.float32, DType.bool](
            out_ptr,
            _get_buffer_ptr[DType.float32](in_buffer),
            size,
            ctx,
        )
    elif dtype == DType.float64:
        unary_mixed_op[op, DType.float64, DType.bool](
            out_ptr,
            _get_buffer_ptr[DType.float64](in_buffer),
            size,
            ctx,
        )
    elif dtype == DType.bfloat16:
        unary_mixed_op[op, DType.bfloat16, DType.bool](
            out_ptr,
            _get_buffer_ptr[DType.bfloat16](in_buffer),
            size,
            ctx,
        )
    else:
        raise Error(
            "Unsupported dtype for unary predicate operation: " + String(dtype)
        )


# =============================================================================
# Kernel implementations
# =============================================================================


@always_inline
def unary_elementwise_op[
    op: ElementwiseUnaryOp, dtype: DType
](
    out_ptr: UnsafePointer[Scalar[dtype], MutExternalOrigin],
    in_ptr: UnsafePointer[Scalar[dtype], MutExternalOrigin],
    size: Int,
    ctx: DeviceContext,
) raises:
    """Elementwise unary operation: out = op(input).

    Parameters:
        op: The unary elementwise operation to perform.
        dtype: The data type of the arrays.

    Args:
        out_ptr: Pointer to the output buffer data.
        in_ptr: Pointer to the input buffer data.
        size: Number of elements to process.
        ctx: Device context.
    """

    @always_inline
    @parameter
    @__copy_capture(out_ptr, in_ptr)
    def func[width: Int, rank: Int, alignment: Int = 1](idx: IndexList[rank]):
        var i = rebind[IndexList[1]](idx)[0]

        var res = op.elementwise(in_ptr.load[width=width](i))
        out_ptr.store[width=width](i, res)

    if ctx.api() == "cpu":
        elementwise[func, simd_width=simd_width_of[dtype]()](
            IndexList[1](size), ctx
        )
    else:
        # GPU execution - check GPU availability and op/dtype support
        comptime if has_accelerator():
            comptime if _is_gpu_allowed_unary_op[
                op
            ]() and dtype != DType.float64:
                elementwise[func, simd_width=1, target="gpu"](
                    IndexList[1](size), ctx
                )
            else:
                raise Error(
                    "GPU execution not supported for this unary elementwise"
                    " op or dtype"
                )
        else:
            raise Error("No GPU accelerator available")


@always_inline
def unary_mixed_op[
    op: ElementwiseUnaryMixedOp, dtype: DType, out_dtype: DType
](
    out_ptr: UnsafePointer[Scalar[out_dtype], MutExternalOrigin],
    in_ptr: UnsafePointer[Scalar[dtype], MutExternalOrigin],
    size: Int,
    ctx: DeviceContext,
) raises:
    """Elementwise unary mixed-type operation: out = op(input).

    Parameters:
        op: The unary mixed-type elementwise operation to perform.
        dtype: The input data type.
        out_dtype: The output data type.

    Args:
        out_ptr: Pointer to the output buffer data.
        in_ptr: Pointer to the input buffer data.
        size: Number of elements to process.
        ctx: Device context.
    """

    @always_inline
    @parameter
    @__copy_capture(out_ptr, in_ptr)
    def func[width: Int, rank: Int, alignment: Int = 1](idx: IndexList[rank]):
        var i = rebind[IndexList[1]](idx)[0]

        var res = op.elementwise[dtype, out_dtype, width](
            in_ptr.load[width=width](i)
        )
        out_ptr.store[width=width](i, res)

    if ctx.api() == "cpu":
        elementwise[func, simd_width=simd_width_of[dtype]()](
            IndexList[1](size), ctx
        )
    else:
        # GPU execution - check GPU availability and op/dtype support
        comptime if has_accelerator():
            comptime if _is_gpu_allowed_mixed_unary_op[
                op
            ]() and dtype != DType.float64:
                elementwise[func, simd_width=1, target="gpu"](
                    IndexList[1](size), ctx
                )
            else:
                raise Error(
                    "GPU execution not supported for this mixed-type unary"
                    " op or dtype"
                )
        else:
            raise Error("No GPU accelerator available")
