# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #

"""Mojo kernel wrappers for matmul MO interpreter operations."""

from std.os import abort
from std.python import PythonObject
from std.python.bindings import PythonModuleBuilder
from std.sys.info import has_accelerator

from std.algorithm.functional import IndexList
from linalg.matmul import matmul
from layout import Coord, Idx, TileTensor, row_major
from std.gpu.host import DeviceContext
from extensibility import (
    ManagedTensorSlice,
)
from extensibility import Input, _FusedComputeOutput
from extensibility import StaticTensorSpec
from builtin_kernels import BatchMatmul as BatchMatmulKernel

from op_utils import _get_dtype, _get_buffer_ptr, _get_ctx, _get_shape, MAX_RANK


def _is_gpu_allowed_matmul_dtype[dtype: DType]() -> Bool:
    """Check if a dtype is allowed for GPU matmul at compile time.

    GPU matmul does not support int8, uint8, int16, uint16, or float64.
    """

    # TODO(MXF-109): Add support for other dtypes.
    return (
        dtype == DType.float32
        or dtype == DType.float16
        or dtype == DType.bfloat16
    )


# =============================================================================
# Python bindings
# =============================================================================


@export
def PyInit_matmul_ops() -> PythonObject:
    """Create a Python module with matmul kernel function bindings."""
    try:
        var b = PythonModuleBuilder("matmul_ops")

        b.def_function[matmul_dispatcher](
            "Matmul", docstring="Matrix multiplication"
        )
        b.def_function[batch_matmul_dispatcher](
            "BatchMatmul", docstring="Batched matrix multiplication"
        )
        b.def_function[batch_matmul_shape_dispatcher](
            "BatchMatmulShape",
            docstring="Compute batch matmul output shape",
        )

        return b.finalize()
    except e:
        abort(t"failed to create matmul op bindings module: {e}")


# =============================================================================
# Dispatcher
# =============================================================================


def matmul_dispatcher(
    out_buffer: PythonObject,
    lhs_buffer: PythonObject,
    rhs_buffer: PythonObject,
    device_context_ptr: PythonObject,
) raises:
    """Matmul dispatcher with dtype dispatch.

    Args:
        out_buffer: The output buffer object.
        lhs_buffer: The left-hand side buffer object.
        rhs_buffer: The right-hand side buffer object.
        device_context_ptr: Device context pointer.
    """
    var dtype = _get_dtype(lhs_buffer)
    var rhs_dtype = _get_dtype(rhs_buffer)
    if dtype != rhs_dtype:
        raise Error(
            "Mismatched input dtypes for matmul: "
            + String(dtype)
            + " and "
            + String(rhs_dtype)
        )

    # Extract shapes: lhs is (M, K), rhs is (K, N), out is (M, N)
    var lhs_shape = lhs_buffer.shape
    var m = Int(py=lhs_shape[0])
    var k = Int(py=lhs_shape[1])
    var rhs_shape = rhs_buffer.shape
    var n = Int(py=rhs_shape[1])

    var ctx = _get_ctx(device_context_ptr)

    # Float types
    if dtype == DType.float16:
        matmul_op[DType.float16](
            _get_buffer_ptr[DType.float16](out_buffer),
            _get_buffer_ptr[DType.float16](lhs_buffer),
            _get_buffer_ptr[DType.float16](rhs_buffer),
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.float32:
        matmul_op[DType.float32](
            _get_buffer_ptr[DType.float32](out_buffer),
            _get_buffer_ptr[DType.float32](lhs_buffer),
            _get_buffer_ptr[DType.float32](rhs_buffer),
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.float64:
        matmul_op[DType.float64](
            _get_buffer_ptr[DType.float64](out_buffer),
            _get_buffer_ptr[DType.float64](lhs_buffer),
            _get_buffer_ptr[DType.float64](rhs_buffer),
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.bfloat16:
        matmul_op[DType.bfloat16](
            _get_buffer_ptr[DType.bfloat16](out_buffer),
            _get_buffer_ptr[DType.bfloat16](lhs_buffer),
            _get_buffer_ptr[DType.bfloat16](rhs_buffer),
            m,
            k,
            n,
            ctx,
        )
    # Integer types
    elif dtype == DType.int8:
        matmul_op[DType.int8](
            _get_buffer_ptr[DType.int8](out_buffer),
            _get_buffer_ptr[DType.int8](lhs_buffer),
            _get_buffer_ptr[DType.int8](rhs_buffer),
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.int16:
        matmul_op[DType.int16](
            _get_buffer_ptr[DType.int16](out_buffer),
            _get_buffer_ptr[DType.int16](lhs_buffer),
            _get_buffer_ptr[DType.int16](rhs_buffer),
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.int32:
        matmul_op[DType.int32](
            _get_buffer_ptr[DType.int32](out_buffer),
            _get_buffer_ptr[DType.int32](lhs_buffer),
            _get_buffer_ptr[DType.int32](rhs_buffer),
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.int64:
        matmul_op[DType.int64](
            _get_buffer_ptr[DType.int64](out_buffer),
            _get_buffer_ptr[DType.int64](lhs_buffer),
            _get_buffer_ptr[DType.int64](rhs_buffer),
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.uint8:
        matmul_op[DType.uint8](
            _get_buffer_ptr[DType.uint8](out_buffer),
            _get_buffer_ptr[DType.uint8](lhs_buffer),
            _get_buffer_ptr[DType.uint8](rhs_buffer),
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.uint16:
        matmul_op[DType.uint16](
            _get_buffer_ptr[DType.uint16](out_buffer),
            _get_buffer_ptr[DType.uint16](lhs_buffer),
            _get_buffer_ptr[DType.uint16](rhs_buffer),
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.uint32:
        matmul_op[DType.uint32](
            _get_buffer_ptr[DType.uint32](out_buffer),
            _get_buffer_ptr[DType.uint32](lhs_buffer),
            _get_buffer_ptr[DType.uint32](rhs_buffer),
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.uint64:
        matmul_op[DType.uint64](
            _get_buffer_ptr[DType.uint64](out_buffer),
            _get_buffer_ptr[DType.uint64](lhs_buffer),
            _get_buffer_ptr[DType.uint64](rhs_buffer),
            m,
            k,
            n,
            ctx,
        )
    else:
        raise Error("Unsupported dtype for matmul: " + String(dtype))


# =============================================================================
# Kernel implementation
# =============================================================================


@always_inline
def matmul_op[
    dtype: DType
](
    out_ptr: UnsafePointer[Scalar[dtype], MutExternalOrigin],
    lhs_ptr: UnsafePointer[Scalar[dtype], MutExternalOrigin],
    rhs_ptr: UnsafePointer[Scalar[dtype], MutExternalOrigin],
    m: Int,
    k: Int,
    n: Int,
    ctx: DeviceContext,
) raises:
    """Matrix multiplication: out = lhs @ rhs.

    Parameters:
        dtype: The data type of the arrays.

    Args:
        out_ptr: Pointer to the output buffer data.
        lhs_ptr: Pointer to the left-hand side buffer data.
        rhs_ptr: Pointer to the right-hand side buffer data.
        m: Number of rows in lhs and output.
        k: Number of columns in lhs / rows in rhs.
        n: Number of columns in rhs and output.
        ctx: Device context.
    """
    var c = TileTensor(out_ptr, row_major(Coord(m, n)))
    var a = TileTensor(lhs_ptr, row_major(Coord(m, k)))
    var b = TileTensor(rhs_ptr, row_major(Coord(k, n)))

    if ctx.api() == "cpu":
        matmul[target="cpu"](c, a, b, ctx)
    else:
        # GPU execution - check GPU availability and dtype support
        comptime if has_accelerator():
            comptime if _is_gpu_allowed_matmul_dtype[dtype]():
                matmul[target="gpu"](
                    c,
                    a,
                    b,
                    ctx,
                )
                # TODO(MXF-108): Remove device sync
                ctx.synchronize()
            else:
                raise Error(
                    "GPU execution not supported for matmul with dtype "
                    + String(dtype)
                )
        else:
            raise Error("No GPU accelerator available")


# =============================================================================
# Batch matmul shape computation
# =============================================================================


def batch_matmul_shape_dispatcher(
    lhs_buffer: PythonObject,
    rhs_buffer: PythonObject,
) raises -> PythonObject:
    """Compute batch matmul output shape using BatchMatmul.shape.

    Delegates to BatchMatmul.shape from the `kernels` package for validation
    and shape computation.

    Args:
        lhs_buffer: The left-hand side buffer object.
        rhs_buffer: The right-hand side buffer object.

    Returns:
        The output shape as a Python list.
    """
    var lhs_shape = lhs_buffer.shape
    var rhs_shape = rhs_buffer.shape
    var rank = Int(py=len(lhs_shape))
    var rhs_rank = Int(py=len(rhs_shape))

    if rank != rhs_rank:
        raise Error(
            "Mismatched ranks for batch_matmul: "
            + String(rank)
            + " and "
            + String(rhs_rank)
        )

    if rank < 3 or rank > MAX_RANK:
        raise Error(
            "batch_matmul requires 3 <= rank <= "
            + String(MAX_RANK)
            + ", got "
            + String(rank)
        )

    # Extract shapes into InlineArrays
    var a_dims = _get_shape(lhs_shape, rank)
    var b_dims = _get_shape(rhs_shape, rank)

    # Dispatch on rank to call batched_matmul_shape with compile-time rank
    from std.python import Python

    var out_shape = Python.evaluate("[]")
    if rank == 3:
        var result = _batch_matmul_shape_for_rank[3](a_dims, b_dims)
        for i in range(3):
            _ = out_shape.append(result[i])
    elif rank == 4:
        var result = _batch_matmul_shape_for_rank[4](a_dims, b_dims)
        for i in range(4):
            _ = out_shape.append(result[i])
    elif rank == 5:
        var result = _batch_matmul_shape_for_rank[5](a_dims, b_dims)
        for i in range(5):
            _ = out_shape.append(result[i])
    else:
        raise Error("Unsupported rank for batch_matmul: " + String(rank))

    return out_shape


def _batch_matmul_shape_for_rank[
    rank: Int
](
    a_dims: InlineArray[Int, MAX_RANK],
    b_dims: InlineArray[Int, MAX_RANK],
) raises -> IndexList[rank]:
    """Call BatchMatmul.shape with compile-time rank.

    Creates shape-only InputTensors (null data pointers) since
    BatchMatmul.shape only inspects shape metadata.

    Parameters:
        rank: The compile-time rank of the tensors.

    Args:
        a_dims: Shape dimensions of the lhs tensor.
        b_dims: Shape dimensions of the rhs tensor.

    Returns:
        The output shape as an IndexList.
    """
    var a_shape = IndexList[rank]()
    var b_shape = IndexList[rank]()
    for i in range(rank):
        a_shape[i] = a_dims[i]
        b_shape[i] = b_dims[i]

    # Create shape-only InputTensors with placeholder pointers — the
    # shape kernel never dereferences the data, so a dangling pointer
    # is safe here.
    comptime spec = StaticTensorSpec[DType.float32, rank, ...].get_unknown()
    var placeholder_ptr = UnsafePointer[
        Scalar[DType.float32], MutAnyOrigin
    ].unsafe_dangling()
    var a = ManagedTensorSlice[io_spec=Input, static_spec=spec](
        placeholder_ptr, a_shape
    )
    var b = ManagedTensorSlice[io_spec=Input, static_spec=spec](
        placeholder_ptr, b_shape
    )

    return BatchMatmulKernel.shape[rank, DType.float32, DType.float32](a, b)


# =============================================================================
# Batch matmul dispatcher
# =============================================================================


def batch_matmul_dispatcher(
    out_buffer: PythonObject,
    lhs_buffer: PythonObject,
    rhs_buffer: PythonObject,
    device_context_ptr: PythonObject,
) raises:
    """Batch matmul dispatcher with dtype dispatch.

    Collapses all batch dimensions into a single batch dimension to produce
    rank-3 tensors, then dispatches to the batched_matmul kernel.

    Args:
        out_buffer: The output buffer object.
        lhs_buffer: The left-hand side buffer object.
        rhs_buffer: The right-hand side buffer object.
        device_context_ptr: Device context pointer.
    """
    var dtype = _get_dtype(lhs_buffer)
    var rhs_dtype = _get_dtype(rhs_buffer)
    if dtype != rhs_dtype:
        raise Error(
            "Mismatched input dtypes for batch_matmul: "
            + String(dtype)
            + " and "
            + String(rhs_dtype)
        )

    # Extract shapes and collapse batch dimensions
    var lhs_shape = lhs_buffer.shape
    var rhs_shape = rhs_buffer.shape
    var rank = Int(py=len(lhs_shape))

    if rank < 3:
        raise Error("batch_matmul requires rank >= 3, got " + String(rank))

    # Compute collapsed batch dim: B = product of all batch dims
    var batch_size = 1
    for i in range(rank - 2):
        batch_size *= Int(py=lhs_shape[i])

    var m = Int(py=lhs_shape[rank - 2])
    var k = Int(py=lhs_shape[rank - 1])
    var n = Int(py=rhs_shape[rank - 1])

    var ctx = _get_ctx(device_context_ptr)

    # Float types
    if dtype == DType.float16:
        batch_matmul_op[DType.float16](
            _get_buffer_ptr[DType.float16](out_buffer),
            _get_buffer_ptr[DType.float16](lhs_buffer),
            _get_buffer_ptr[DType.float16](rhs_buffer),
            batch_size,
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.float32:
        batch_matmul_op[DType.float32](
            _get_buffer_ptr[DType.float32](out_buffer),
            _get_buffer_ptr[DType.float32](lhs_buffer),
            _get_buffer_ptr[DType.float32](rhs_buffer),
            batch_size,
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.float64:
        batch_matmul_op[DType.float64](
            _get_buffer_ptr[DType.float64](out_buffer),
            _get_buffer_ptr[DType.float64](lhs_buffer),
            _get_buffer_ptr[DType.float64](rhs_buffer),
            batch_size,
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.bfloat16:
        batch_matmul_op[DType.bfloat16](
            _get_buffer_ptr[DType.bfloat16](out_buffer),
            _get_buffer_ptr[DType.bfloat16](lhs_buffer),
            _get_buffer_ptr[DType.bfloat16](rhs_buffer),
            batch_size,
            m,
            k,
            n,
            ctx,
        )
    # Integer types
    elif dtype == DType.int8:
        batch_matmul_op[DType.int8](
            _get_buffer_ptr[DType.int8](out_buffer),
            _get_buffer_ptr[DType.int8](lhs_buffer),
            _get_buffer_ptr[DType.int8](rhs_buffer),
            batch_size,
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.int16:
        batch_matmul_op[DType.int16](
            _get_buffer_ptr[DType.int16](out_buffer),
            _get_buffer_ptr[DType.int16](lhs_buffer),
            _get_buffer_ptr[DType.int16](rhs_buffer),
            batch_size,
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.int32:
        batch_matmul_op[DType.int32](
            _get_buffer_ptr[DType.int32](out_buffer),
            _get_buffer_ptr[DType.int32](lhs_buffer),
            _get_buffer_ptr[DType.int32](rhs_buffer),
            batch_size,
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.int64:
        batch_matmul_op[DType.int64](
            _get_buffer_ptr[DType.int64](out_buffer),
            _get_buffer_ptr[DType.int64](lhs_buffer),
            _get_buffer_ptr[DType.int64](rhs_buffer),
            batch_size,
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.uint8:
        batch_matmul_op[DType.uint8](
            _get_buffer_ptr[DType.uint8](out_buffer),
            _get_buffer_ptr[DType.uint8](lhs_buffer),
            _get_buffer_ptr[DType.uint8](rhs_buffer),
            batch_size,
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.uint16:
        batch_matmul_op[DType.uint16](
            _get_buffer_ptr[DType.uint16](out_buffer),
            _get_buffer_ptr[DType.uint16](lhs_buffer),
            _get_buffer_ptr[DType.uint16](rhs_buffer),
            batch_size,
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.uint32:
        batch_matmul_op[DType.uint32](
            _get_buffer_ptr[DType.uint32](out_buffer),
            _get_buffer_ptr[DType.uint32](lhs_buffer),
            _get_buffer_ptr[DType.uint32](rhs_buffer),
            batch_size,
            m,
            k,
            n,
            ctx,
        )
    elif dtype == DType.uint64:
        batch_matmul_op[DType.uint64](
            _get_buffer_ptr[DType.uint64](out_buffer),
            _get_buffer_ptr[DType.uint64](lhs_buffer),
            _get_buffer_ptr[DType.uint64](rhs_buffer),
            batch_size,
            m,
            k,
            n,
            ctx,
        )
    else:
        raise Error("Unsupported dtype for batch_matmul: " + String(dtype))


# =============================================================================
# Batch matmul kernel implementation
# =============================================================================


@always_inline
def batch_matmul_op[
    dtype: DType
](
    out_ptr: UnsafePointer[Scalar[dtype], MutExternalOrigin],
    lhs_ptr: UnsafePointer[Scalar[dtype], MutExternalOrigin],
    rhs_ptr: UnsafePointer[Scalar[dtype], MutExternalOrigin],
    batch_size: Int,
    m: Int,
    k: Int,
    n: Int,
    ctx: DeviceContext,
) raises:
    """Batched matrix multiplication: out = lhs @ rhs with batch dims collapsed.

    All batch dimensions are collapsed into a single batch_size dimension,
    so we always operate on rank-3 tensors: (batch_size, M, K) @ (batch_size, K, N).
    Uses BatchMatmul.execute from the `kernels` package.

    Parameters:
        dtype: The data type of the arrays.

    Args:
        out_ptr: Pointer to the output buffer data.
        lhs_ptr: Pointer to the left-hand side buffer data.
        rhs_ptr: Pointer to the right-hand side buffer data.
        batch_size: Product of all batch dimensions.
        m: Number of rows in each matrix of lhs and output.
        k: Number of columns in lhs / rows in rhs (inner dim).
        n: Number of columns in each matrix of rhs and output.
        ctx: Device context.
    """
    # Create rank-3 ManagedTensorSlice wrappers with collapsed batch dimension
    comptime in_spec = StaticTensorSpec[dtype, 3, ...].get_unknown()
    comptime out_spec = StaticTensorSpec[dtype, 3, ...].get_unknown()

    var a = ManagedTensorSlice[io_spec=Input, static_spec=in_spec](
        lhs_ptr, IndexList[3](batch_size, m, k)
    )
    var b = ManagedTensorSlice[io_spec=Input, static_spec=in_spec](
        rhs_ptr, IndexList[3](batch_size, k, n)
    )
    var c = ManagedTensorSlice[
        io_spec=_FusedComputeOutput, static_spec=out_spec
    ](out_ptr, IndexList[3](batch_size, m, n))

    if ctx.api() == "cpu":
        BatchMatmulKernel.execute[
            rank=3,
            lambdas_have_fusion=False,
            transpose_b=False,
            target="cpu",
        ](c, a, b, ctx)
    else:
        comptime if has_accelerator():
            comptime if _is_gpu_allowed_matmul_dtype[dtype]():
                BatchMatmulKernel.execute[
                    rank=3,
                    lambdas_have_fusion=False,
                    transpose_b=False,
                    target="gpu",
                ](c, a, b, ctx)
            else:
                raise Error(
                    "GPU execution not supported for batch_matmul with dtype "
                    + String(dtype)
                )
        else:
            raise Error("No GPU accelerator available")
