# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #
"""The module implements matrix band part functions."""


from std.algorithm.functional import elementwise, unswitch
from std.gpu.host import DeviceContext
from layout import TileTensor


from std.utils.index import IndexList


@always_inline
def matrix_band_part[
    dtype: DType,
    int_type: DType,
    cond_type: DType,
    rank: Int,
    simd_width: Int,
    InputFnType: ImplicitlyCopyable
    & RegisterPassable
    & def[width: Int, rank: Int](IndexList[rank]) -> SIMD[dtype, width],
    target: StaticString = "cpu",
](
    input_0_fn: InputFnType,
    input_shape: IndexList[rank],
    num_lower: TileTensor[dtype=int_type, ...],
    num_upper: TileTensor[dtype=int_type, ...],
    exclude: TileTensor[dtype=cond_type, ...],
    output: TileTensor[mut=True, dtype=dtype, ...],
    ctx: DeviceContext,
) raises:
    var lower_diagonal_index = Int(num_lower.load_linear[1](IndexList[1](0)))
    var upper_diagonal_index = Int(num_upper.load_linear[1](IndexList[1](0)))

    @__copy_capture(
        input_shape,
        lower_diagonal_index,
        upper_diagonal_index,
        output,
        input_0_fn,
    )
    @parameter
    def dispatch[exclude: Bool]() raises:
        _matrix_band_part_impl[
            dtype,
            int_type,
            cond_type,
            rank,
            simd_width,
            exclude=exclude,
            target=target,
        ](
            input_0_fn,
            input_shape,
            lower_diagonal_index,
            upper_diagonal_index,
            output,
            ctx,
        )

    unswitch[dispatch](exclude.load_linear[1](IndexList[1](0)) != 0)


@always_inline
def _matrix_band_part_impl[
    dtype: DType,
    int_type: DType,
    cond_type: DType,
    rank: Int,
    simd_width: Int,
    InputFnType: ImplicitlyCopyable
    & RegisterPassable
    & def[width: Int, rank: Int](IndexList[rank]) -> SIMD[dtype, width],
    exclude: Bool,
    target: StaticString = "cpu",
](
    input_0_fn: InputFnType,
    input_shape: IndexList[rank],
    lower_diagonal_index: Int,
    upper_diagonal_index: Int,
    output: TileTensor[mut=True, dtype=dtype, ...],
    ctx: DeviceContext,
) raises:
    comptime assert rank >= 2, "Matrix band only supports rank >=2"

    @always_inline
    def func[
        simd_width: Int, inner_rank: Int, alignment: Int = 1
    ](index: IndexList[inner_rank]) {
        var lower_diagonal_index,
        var upper_diagonal_index,
        var output,
        var input_0_fn,
    }:
        var idx = rebind[IndexList[rank]](index)

        var row = idx[rank - 2]
        var col = idx[rank - 1]

        var in_band = (
            lower_diagonal_index < 0 or (row - col) <= lower_diagonal_index
        ) and (upper_diagonal_index < 0 or (col - row) <= upper_diagonal_index)

        comptime if exclude:
            in_band = not in_band

        if in_band:
            output.store_linear(
                idx, rebind[Scalar[dtype]](input_0_fn[simd_width, rank](idx))
            )
        else:
            output.store_linear(idx, Scalar[dtype](0))

    elementwise[
        simd_width=1,
        target=target,
    ](func, input_shape, ctx)
